/*
* Copyright (C) 2012 Doubango Telecom <http://www.doubango.org>
* License: BSD
* This file is part of Open Source sipML5 solution <http://www.sipml5.org>
*/
tsip_header_Refer_Sub.prototype = Object.create(tsip_header.prototype);
%%{
	machine tsip_machine_parser_header_Refer_Sub;

	# Includes
	include tsip_machine_utils "./tsip_machine_utils.jrl";
	
	action tag{
		i_tag_start = p;
	}

	action is_true{
		hdr_rsub.b_sub = true;
	}
	action is_false{
		hdr_rsub.b_sub = false;
	}
	action parse_param{
		tsk_ragel_add_param(s_str, p, i_tag_start, hdr_rsub.ao_params);
	}

	action eob{
	}
	
	refer_sub_value = "true"i>tag %is_true | "false"i>tag %is_false;
	exten = generic_param>tag %parse_param;
	Refer_Sub = "Refer-Sub"i HCOLON refer_sub_value ( SEMI exten )*;

	# Entry point
	main := Refer_Sub :>CRLF @eob;

}%%

%%write data;

function tsip_header_Refer_Sub(b_sub){
	tsip_header.call(this, tsip_header_type_e.Refer_Sub);
    this.b_sub = b_sub;
}

tsip_header_Refer_Sub.prototype.toString = function(){
    return this.b_sub ? "true" : "false";
}

tsip_header_Refer_Sub.prototype.Parse = function(s_str){
    var cs = 0;
	var p = 0;
	var pe = s_str.length;
	var eof = pe;
	var data = tsk_buff_str2ib(s_str);
	var i_tag_start;	
	var hdr_rsub = new tsip_header_Refer_Sub(true);
	
	%%write init;
	%%write exec;
	
	if( cs < %%{ write first_final; }%% ){
		tsk_utils_log_error("Failed to parse 'Refer-Sub' header: " + s_str);
		return null;
	}
	
	return hdr_rsub;
}
