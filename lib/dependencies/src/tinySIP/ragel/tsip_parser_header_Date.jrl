/*
* Copyright (C) 2012 Doubango Telecom <http://www.doubango.org>
* License: BSD
* This file is part of Open Source sipML5 solution <http://www.sipml5.org>
*/
tsip_header_Date.prototype = Object.create(tsip_header.prototype);
%%{
	machine tsip_machine_parser_header_Date;

	# Includes
	include tsip_machine_utils "./tsip_machine_utils.jrl";
	
	action tag{
		i_tag_start = p;
	}

	action parse_wkday{
	    hdr_date.s_wkday = tsk_ragel_parser_get_string(s_str, p, i_tag_start);
	}

	action parse_day{
	    hdr_date.i_day = tsk_ragel_parser_get_int(s_str, p, i_tag_start);
	}

	action parse_month{
	    hdr_date.s_month= tsk_ragel_parser_get_string(s_str, p, i_tag_start);
	}

	action parse_year{
	    hdr_date.i_year = tsk_ragel_parser_get_int(s_str, p, i_tag_start);
	}

	action parse_h{
	    hdr_date.time.i_h = tsk_ragel_parser_get_int(s_str, p, i_tag_start);
	}

	action parse_m{
	    hdr_date.time.i_m = tsk_ragel_parser_get_int(s_str, p, i_tag_start);
	}

	action parse_s{
		hdr_date.time.i_s = tsk_ragel_parser_get_int(s_str, p, i_tag_start);
	}

	action eob{
	}
		
	wkday = "Mon"i | "Tue"i | "Wed"i | "Thu"i | "Fri"i | "Sat"i | "Sun"i;
	month = "Jan"i | "Feb"i | "Mar"i | "Apr"i | "May"i | "Jun"i | "Jul"i | "Aug"i | "Sep"i | "Oct"i | "Nov"i | "Dec"i;
	date1 = DIGIT{2}>tag %parse_day SP month>tag %parse_month SP DIGIT{4}>tag %parse_year;
	time = DIGIT{2}>tag %parse_h ":" DIGIT{2}>tag %parse_m ":" DIGIT{2}>tag %parse_s;
	rfc1123_date = wkday>tag %parse_wkday "," SP date1 SP time SP "GMT"i;
	SIP_date = rfc1123_date;
	Date = "Date"i HCOLON SIP_date;
	
	# Entry point
	main := Date :>CRLF @eob;

}%%

%%write data;

function tsip_header_Date(s_wkday, s_month, i_day, i_year, i_h, i_m, i_s){
	tsip_header.call(this, tsip_header_type_e.Date);
    this.s_wkday = s_wkday;
    this.s_month = s_month;
    this.i_day = i_day;
    this.i_year = i_year;
    this.time = {};
    this.time.i_h = i_h;
    this.time.i_m = i_m;
    this.time.i_s = i_s;
    // Date: Wed, 28 Apr 2010 23:42:50 GMT
}

tsip_header_Date.prototype.toString = function(){
    if(this.s_month){
        return tsk_string_format("{0}, {1} {2} {3} {4}:{5}:{6} GMT",
            this.s_wkday, this.i_day, this.s_month, this.i_year, this.time.i_h, this.time.i_m, this.time.i_s);
    }
    return null;
}

tsip_header_Date.prototype.Parse = function(s_str){
    var cs = 0;
	var p = 0;
	var pe = s_str.length;
	var eof = pe;
	var data = tsk_buff_str2ib(s_str);
	var i_tag_start;	
	var hdr_date = new tsip_header_Date(null, null, -1, -1, -1, -1, -1);
	
	%%write init;
	%%write exec;
	
	if( cs < %%{ write first_final; }%% ){
		tsk_utils_log_error("Failed to parse 'Date' header: " + s_str);
		return null;
	}
	
	return hdr_date;
}