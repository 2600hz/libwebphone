/*
* Copyright (C) 2012 Doubango Telecom <http://www.doubango.org>
* License: BSD
* This file is part of Open Source sipML5 solution <http://www.sipml5.org>
*/
%%{
	machine tsip_machine_message;

	#message_body = OCTET*;
	message_body = js_any*;
	SIP_Version = ("SIP"i "/" DIGIT+ "." DIGIT+) >tag %parse_sipversion;
	
	action prev_not_comma{
		prev_not_comma(o_ragel_state, p)
	}

	message_header = ((token) SP* HCOLON SP*<: ((js_any+ - CRLF) (COMMA CRLF SP)?)* CRLF) >tag %parse_header;

	# SIP RESPONSE
	Reason_Phrase = (( reserved | unreserved | escaped | UTF8_NONASCII | UTF8_CONT | SP | HTAB )*)>tag %parse_reason_phrase;
	Status_Line = SIP_Version :>SP Status_Code>tag %parse_status_code :>SP Reason_Phrase :>CRLF;
	Response = Status_Line (message_header* CRLF);


	# SIP REQUEST
	URI = (scheme HCOLON js_any+)>tag %parse_requesturi;
	Request_URI = URI;
	Request_Line = Method>tag %parse_method :>SP Request_URI :>SP SIP_Version :>CRLF;
	Request = Request_Line (message_header* CRLF);

	# SIP MESSAGE
	SIP_message = (Response | Request)>1 @eoh message_body?>0;

}%%
